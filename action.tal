( constants )

%gravity { #04 }
%thrust { #40 }

( devices )

|00 @System     [ &vector $2 &pad    $6 &r      $2 &g   $2 &b $2 ]
|20 @Screen     [ &vector $2 &width  $2 &height $2 &pad $2 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1 ]
|80 @Controller [ &vector $2 &button $1 &key    $1 ] 

( variables )

|0000

@player [ &y $2 &v $1 ]

( program )

|0100 ( -> )

    ( theme )
    #2fe5 .System/r DEO2
    #2fc5 .System/g DEO2
    #4f25 .System/b DEO2

    ( initialize )
    #08 #00 .player/y STZ2
    thrust .player/v STZ

    ( vectors )
    ;on-frame .Screen/vector DEO2

    BRK

@on-frame ( -> )
    ;sprite .Screen/addr DEO2

    ( clear )
    #00 ;draw-player JSR2

    ( move player )
    ;move-player JSR2

    ( draw )
    #01 ;draw-player JSR2

    BRK

@move-player ( -- )
    ( reset player/v on up-key press )
    ;get-up-key JSR2
    #01 NEQ ,&no-up JCN
    thrust .player/v STZ
    &no-up

    ( v += gravity )
    .player/v LDZ
    gravity ADD
    DUP gravity LTH #ff MUL ORA ( prevent overflow )
    .player/v STZ

    ( y += v - 0x80 )
    .player/y LDZ2
    #00 .player/v LDZ ADD2
    #00 #80 SUB2
    .player/y STZ2

    JMP2r

@get-up-key ( -- up )
    .Controller/button DEI #04 SFT #01 AND
    JMP2r

@draw-player ( color -- )
    #01 #00 .Screen/x DEO2
    .player/y LDZ2 #04 SFT2 .Screen/y DEO2
    .Screen/sprite DEO
    JMP2r

@sprite
    3c7e ffff ffff 7e3c
