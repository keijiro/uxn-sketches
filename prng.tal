( devices )

|00 @System [ &vector $2 &pad   $6 &r      $2 &g   $2 &b $2 ]
|20 @Screen [ &vector $2 &width $2 &height $2 &pad $2 &x $2 &y $2 &addr $2 &pixel $1 &sprite $1 ]

( variables )

|0000

( program )

|0100 ( -> )

    ( theme )
    #0fff .System/r DEO2
    #0fff .System/g DEO2
    #0fff .System/b DEO2

    ( random seed )
    #01 ;rand/a STA

    ( vector )
	;on-frame .Screen/vector DEO2

    BRK

@on-frame ( -- )
    #00 ,&y STR
    &loop-y
        #00 ,&y LDR .Screen/y DEO2
        #00 ,&x STR
        ;rand JSR2
        &loop-x
            ,&x LDR GTHk .Screen/pixel DEO
            #00 SWP .Screen/x DEO2
            ,&x LDR INC DUP ,&x STR
            ,&loop-x JCN
        POP
        ,&y LDR INC DUP ,&y STR
        ,&loop-y JCN
    BRK

    &x $1 &y $1

@rand ( -- number )
    ( 8-bit PRNG https://github.com/edrosten/8bit_rng )

    ( t = x ^ (x << 4) )
    ,&x LDR DUP #40 SFT EOR
    ( x = y )
    ,&y LDR ,&x STR
    ( y = z )
    ,&z LDR ,&y STR
    ( z = a )
    ,&a LDR DUP ,&z STR
    ( a = z ^ t ^ (z >> 1) ^ (t << 1) )
    DUP #10 SFT EOR SWP DUP #01 SFT EOR EOR
    DUP ,&a STR
    JMP2r

    &x $1 &y $1 &z $1 &a $1
